<form version="1.1">
  <label>Search For Dashboard</label>
  <description>Dashboard to Search For Specific Dashboards</description>
  <fieldset submitButton="false">
    <input type="text" token="c-input">
      <label>Enter Search Terms</label>
    </input>
  </fieldset>
  <row>
    <panel>
      <table>
        <search>
          <query>| rest splunk_server=local /servicesNS/-/-/data/ui/views
| search eai:acl.app=* AND author!="nobody"
| lookup dashboard_details id as id output details, mitre, usecase
| fillnull value="TBD" usecase, mitre
| rex field="id" "(?&lt;urlField&gt;[^\/]+)$"

``` extract sourcetype, source, or eventtype field &amp; values; exclude negated matches ```
| rex field=eai:data "(?&lt;!(?#Skip excluded sourcetypes)\bNOT\s)(?i)(?&lt;sourcetypes&gt;sourcetype(?:\s*(?:=|::)[\s\x22]*[-.:\w\x2a]+|\s+IN\s+\x28[-.:\w,\s]+\x29))" max_match=0
| rex field=eai:data "(?&lt;!(?#Skip excluded sources)\bNOT\s)(?i)(?&lt;sources&gt;source(?:\s*(?:=|::)[\s\x22]*[-.:\w\x2a]+|\s+IN\s+\x28[-.:\w,\s]+\x29))" max_match=0
| rex field=eai:data "(?&lt;!(?#Skip excluded eventtypes)\bNOT\s)(?i)(?&lt;eventtypes&gt;eventtype(?:\s*(?:=|::)[\s\x22]*[-.:\w\x2a]+|\s+IN\s+\x28[-.:\w,\s]+\x29))" max_match=0

``` extract host and index field &amp; values; exclude negated matches ```
| rex field=eai:data "(?#Skip excluded hosts)(?&lt;!\bNOT\s)(?i)(?&lt;hosts&gt;host(?:\s*(?:=|::)[\s\x22]*[-.:\w\x2a]+|\s+IN\s+\x28[-.:\w,\s]+\x29))" max_match=0
| rex field=eai:data "(?#Skip excluded indexes)(?&lt;!\bNOT\s)(?i)(?&lt;indexes&gt;index(?:\s*(?:=|::)[\s\x22]*[-.:\w\x2a]+|\s+IN\s+\x28[-.:\w,\s]+\x29))" max_match=0

``` extract Lookup, InputLookup, and OutputLookup commands &amp; values; must always be after a pipe character ```
| rex field=eai:data "(?i)\x7c\s*(?&lt;lookups&gt;lookup\b[\s\x22]+[-.:\w]+)" max_match=0
| rex field=eai:data "(?i)\x7c\s*(?&lt;inputlookups&gt;inputlookup\b[\s\x22]+[-.:\w]+)" max_match=0
| rex field=eai:data "(?i)\x7c\s*(?&lt;outputlookups&gt;outputlookup\b[\s\x22]+[-.:\w]+)" max_match=0

``` extract the whole query ```
| rex field=eai:data "(?s)&lt;query&gt;(?&lt;queries&gt;.*?)&lt;\/query&gt;.*?" max_match=0

``` Trim extraneous double quotes from captured fields ```
| rex mode=sed field=sourcetypes "s/\x22//g"
| rex mode=sed field=sources "s/\x22//g"
| rex mode=sed field=eventtypes "s/\x22//g"
| rex mode=sed field=hosts "s/\x22//g"
| rex mode=sed field=indexes "s/\x22//g"
| rex mode=sed field=lookups "s/\x22//g"
| rex mode=sed field=inputlookups "s/\x22//g"
| rex mode=sed field=outputlookups "s/\x22//g"

| eval datasources=mvdedup(mvappend(sourcetypes, sources, eventtypes, indexes, hosts, lookups, inputlookups, outputlookups))
| table queries, sources, sourcetypes, eventtypes, datasources, app.owner, urlField, eai:acl.app author, eai:acl.sharing details, mitre, usecase

| rename eai:acl.app as myapp

| appendcols

  [ search index=summary source="dashboard_views"
  | table myapp, file, method, status,  user
  | stats dc(user) as dc_user count by myapp, file
  | rename file as urlField
  | table myapp, urlField, count, dc_user
  ]
| search urlField="*$c-input$*"  OR datasources="*$c-input$*" OR eai.data="*$c-input$*"
| fillnull value="N/A" datasources
| table urlField, datasources, myapp  author, eai:acl.sharing, count, dc_user</query>
          <earliest>-24h@h</earliest>
          <latest>now</latest>
        </search>
        <option name="drilldown">cell</option>
        <option name="refresh.display">progressbar</option>
        <drilldown>
          <condition field="urlField">
            <eval token="u">$row.urlField$</eval>
            <eval token="w">$row.myapp$</eval>
            <link target="_blank">
              <![CDATA[/app/$w$/$u$]]>
            </link>
          </condition>
        </drilldown>
      </table>
    </panel>
  </row>
</form>